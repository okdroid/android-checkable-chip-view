apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


task sourceJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier "sources"
}

publishing {
	publications {
		Production(MavenPublication) {
			artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
			artifact(sourceJar)

			groupId 'com.github.okdroid'
			artifactId 'checkablechipview'
			version '0.0.9'

			pom.withXml {
				def dependencies = asNode().appendNode('dependencies')
				configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
					def dependency = dependencies.appendNode('dependency')
					dependency.appendNode('groupId', it.moduleGroup)
					dependency.appendNode('artifactId', it.moduleName)
					dependency.appendNode('version', it.moduleVersion)
				}
			}
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_API_KEY')
	dryRun = false
	override = false
	publish = true
	pkg {
		repo = 'maven'
		name = 'checkablechipview'
		userOrg = 'markushi'
		licenses = ['Apache-2.0']
		desc = 'Checkable Chipview for Android'
		vcsUrl = 'https://github.com/okdroid/checkable-chip-view.git'

		version {
			name = '0.0.9'
			vcsTag = '0.0.9'
		}
	}
	publications = ['Production']
}